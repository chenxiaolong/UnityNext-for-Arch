# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

desktop_arch=('i686' 'x86_64')
# Let me know what ARM architectures are compatible
mobile_arch=()

pkgname=mir-bzr
pkgver=1050
_real_ver=0.0.3
pkgrel=1
pkgdesc="Ubuntu's new display server"
arch=(${desktop_arch[@]} ${mobile_arch[@]})
url="https://launchpad.net/mir"
# Server is GPL, client is LGPL
license=('GPL' 'LGPL')
groups=('unitynext')

depends=('boost' 'gflags' 'google-glog' 'liburcu' 'lttng-ust-old' 'libxkbcommon' 'protobuf')
# At the moment, Mir does require Mesa's libGL
depends+=('glm' 'mesa-libgl' 'mesa')

if grep -q ${CARCH} <<< ${desktop_arch[@]}; then
  depends+=('libdrm')
fi
if grep -q ${CARCH} <<< ${mobile_arch[@]}; then
  # Currently unpackaged. Presumably, Ubuntu's fork will need to be used
  depends+=('libhybris')
fi

makedepends=('bzr' 'cmake' 'doxygen' 'graphviz' 'libxslt' 'umockdev')
checkdepends=('gmock-ubuntu')

# Provide Ubuntu's library names
provides=("libmirprotobuf=${_real_ver}"     # Protocol shared library
          "libmirserver=${_real_ver}"       # Server shared library
          "libmirclient=${_real_ver}"       # Client shared library
          "libmirclient-demos=${_real_ver}" # Example clients
          "mir=${_real_ver}")               # Stable version of Mir
conflicts=('libmirprotobuf'
           'libmirserver'
           'libmirclient'
           'libmirclient-demos'
           'mir')

source=('mir::bzr+http://bazaar.launchpad.net/~mir-team/mir/trunk/'
        '0001_No_documentation.patch'
        '0002_Remove_death-tests.patch'
        '0003_Build_Fixes.patch')
sha512sums=('SKIP'
            'ef313ff807c6e7134a1f541195fc1cedf969fc22ba2f1220eaa9dc824d3ff934aa88fac1c2a1956f58659d3323b491867cf245c6eba874552eea31c5e3ed2ce7'
            '025849496593b80f204962273bf99dc15e3f886f20dc5a4e6229e75a5473fa9e300b63a2e57bece38ec8a857c9321cf9bb712b69c378a62406692aae4780e6d1'
            '905770069922e47c74c1bf9e33ce2c0a1bc70ac3c6d74e8168bb53c1b8aa55214e9f13606b8a29856bb507a75830f888e75190396be4f31e0933f9c7613fc519')

pkgver() {
  cd mir
  bzr revno
}

prepare() {
  cd "${srcdir}/mir"

  patch -p0 -i "${srcdir}/0001_No_documentation.patch"
  # Causes build to fail on Jenkins for some reason
  #patch -p0 -i "${srcdir}/0002_Remove_death-tests.patch"
  patch -p0 -i "${srcdir}/0003_Build_Fixes.patch"
}

build() {
  cd "${srcdir}/mir"

  COMMON_PARAMS=('-DCMAKE_C_COMPILER=gcc'
                 '-DCMAKE_CXX_COMPILER=g++'
                 '-DCMAKE_INSTALL_PREFIX=/usr'
                 '-DMIR_DISABLE_EPOLL_REACTOR=YES'
                 '-DDISABLE_GTEST_TEST_DISCOVERY=YES' # Otherwise FTB in Jenkins
                 `# Failing to build '-DBUILD_DOXYGEN=YES'`)
  
  if grep -q ${CARCH} <<< ${mobile_arch[@]}; then
    cmake . ${COMMON_PARAMS[@]}         \
            -DBoost_COMPILER=-gcc       \
            -DMIR_PLATFORM=android
  fi

  if grep -q ${CARCH} <<< ${desktop_arch[@]}; then
    cmake . ${COMMON_PARAMS[@]}
  fi

  make
}

check() {
  cd "${srcdir}/mir"
  if grep -q ${CARCH} <<< ${desktop_arch[@]}; then
    GTEST_OUTPUT=xml:./ ctest -j1 || :
  fi
}

package() {
  cd "${srcdir}/mir"
  make DESTDIR="${pkgdir}/" install

  # Install examples
  install -dm755 "${pkgdir}"/usr/share/doc/mir/examples/
  install -m644 examples/basic.c            \
                examples/flicker.c          \
                examples/graphics.h         \
                examples/graphics_utils.cpp \
                examples/mir_image.h        \
                examples/scroll.cpp         \
                examples/README             \
                "${pkgdir}"/usr/share/doc/mir/examples/
}

# vim:set ts=2 sw=2 et:
