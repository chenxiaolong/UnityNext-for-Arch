# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

# Based on Arch Linux commit: 6715baabdc835d2e8f0e82f6c42091dad8c50ce0

pkgname=xf86-video-nouveau-mir
_ubuntu_rel=1ubuntu2
pkgver=1.0.10
pkgrel=1
pkgdesc="Open Source 2D acceleration driver for nVidia cards"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/"
license=('GPL')
depends=('libdrm' 'systemd' 'nouveau-dri-mir')
makedepends=('xorg-server-devel-mir' 'X-ABI-VIDEODRV_VERSION=15')
makedepends+=('xorg-server-mir')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16')
provides+=("xf86-video-nouveau=${pkgver}")
conflicts+=('xf86-video-nouveau')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
install=xf86-video-nouveau.install
source=("http://xorg.freedesktop.org/archive/individual/driver/xf86-video-nouveau-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-video-nouveau_${pkgver}-${_ubuntu_rel}.diff.gz")
sha512sums=('461301e5e05cdf21197abe7d7c009d491aa7d455403a3976d0ad0cba05a8d43010440e60061f5d3bcfe6cfe6bd6c4c7723ed73a889459950b795ea895459b32b'
            '6ea02584431925d54643a587c85a6cfa21d435691f3cb07a4f3b6272a9ca6d46ff30b10fb841ab42cb59d4b5f1a6a6f499949b364b11ba7ccf8ee06193e54b5d')

prepare() {
  cd "${srcdir}/xf86-video-nouveau-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/xserver-xorg-video-nouveau_${pkgver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/xf86-video-nouveau-${pkgver}"
  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xf86-video-nouveau-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
