# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Arch Linux commit: d938f7c8e7adc1aad185b0313262e04ce3a82f38

pkgbase=mesa-mir
# Stupid AUR
url="http://mesa3d.sourceforge.net"
license=('custom')
arch=('i686' 'x86_64')

pkgname=mesa-mir
pkgdesc="Mesa with Mir support"

# Inline comments break the AUR parser... ('ati-dri-mir' ...)
true && pkgname=('ati-dri-mir' 'intel-dri-mir' 'nouveau-dri-mir' 'svga-dri-mir' 'mesa-mir' 'mesa-libgl-mir')

pkgver=9.1.6
_ubuntu_rel=2ubuntu2

pkgrel=1

# (... 'llvm-amdgpu-snapshot')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'libxxf86vm' 'libxdamage' 'libvdpau' 'wayland' 'llvm-amdgpu-snapshot' 'systemd')
# Requires Mir, of course (specifically, libmirclient)
makedepends+=('mir-bzr')
# For filterdiff
makedepends+=('patchutils')

options=('!libtool')
source=("ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/mesa_${pkgver}-${_ubuntu_rel}.diff.gz"
        'LICENSE')
sha512sums=('b23d0cc3515b56636f73c4d3247212f8c731c967f5ecf8c247a3bd359038ed374c23eb1bb0da17a9152f8b8d984acd134fa9823d9349285287e58676419b4d40'
            '2a84b5906250472c4f9eaa9da51e611d9b81f57e53832e434919fc824e5d3da49a87506408359ca33827866fc5f1b23a1d7165184800c6e83f691d1a2076df84'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')

build() {
  cd "${srcdir}/Mesa-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/mesa_${pkgver}-${_ubuntu_rel}.diff"
  #cat "${srcdir}/mesa_${_ubuntu_ver}-${_ubuntu_rel}.diff" | \
  #  filterdiff --include="*/debian/*" | \
  #  patch -p1
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-llvm-shared-libs \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm,wayland,mir \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau

  make

  install -dm755  "${srcdir}/fakeinstall/"
  make DESTDIR="${srcdir}/fakeinstall/" install
}

package_ati-dri-mir() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  depends=("mesa-libgl-mir")
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  provides=("ati-dri=${pkgver}")
  conflicts=('xf86-video-ati<6.9.0-6' 'ati-dri')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_{r300,r600,radeonsi}.* ${pkgdir}/usr/lib/vdpau/

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_{r300,r600,radeonsi}* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}

package_intel-dri-mir() {
  pkgdesc="Mesa drivers for Intel"
  depends=("mesa-libgl-mir")
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  provides=("intel-dri=${pkgver}")
  conflicts=('intel-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_nouveau-dri-mir() {
  pkgdesc="Mesa drivers for Nouveau"
  depends=("mesa-libgl-mir")
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  provides=("nouveau-dri=${pkgver}")
  conflicts=('nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_nouveau.* ${pkgdir}/usr/lib/vdpau/

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/nouveau-dri/"
}

package_svga-dri-mir() {
  pkgdesc="Gallium3D VMware guest GL driver"
  depends=('libdrm' 'expat' 'llvm-amdgpu-lib-snapshot')
  provides=("svga-dri=${pkgver}")
  conflicts=('svga-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/vmwgfx_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_vmwgfx* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/svga-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/svga-dri/"
}

package_mesa-mir() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'systemd')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  provides+=("mesa=${pkgver}")
  conflicts+=('mesa')

  depends+=('mir-bzr')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 	${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_mesa-libgl-mir() {
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa-mir")
  provides=("libgl=${pkgver}" "mesa-libgl=${pkgver}")
  replaces=('libgl' 'mesa-libgl')
  conflicts=('mesa-libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
