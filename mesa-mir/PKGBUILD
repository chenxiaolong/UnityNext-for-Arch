# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on the official Arch Linux PKGBUILD: https://projects.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/mesa

pkgbase=mesa-mir
# Stupid AUR
url="http://mesa3d.sourceforge.net"
license=('custom')
arch=('i686' 'x86_64')

pkgname=mesa-mir
pkgdesc="Mesa with Mir support"

# Inline comments break the AUR parser... ('ati-dri-mir' ...)
true && pkgname=('intel-dri-mir' 'nouveau-dri-mir' 'svga-dri-mir' 'mesa-mir' 'mesa-libgl-mir')

pkgver=9.1.rc2
_ubuntu_ver=9.1~rc2
_ubuntu_rel=0ubuntu0+mir2-jenkins17
_base_rel=1

# AUR Workaround
pkgrel=${_base_rel}.0ubuntu0.mir2

# (... 'llvm-amdgpu-snapshot')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'libxxf86vm' 'libxdamage' 'libvdpau' 'wayland')
# Requires Mir, of course (specifically, libmirclient)
makedepends+=('mir-bzr')
# For filterdiff
makedepends+=('patchutils')

options=('!libtool')
source=(#"ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2"
        "https://launchpad.net/~mir-team/+archive/staging/+files/mesa_${_ubuntu_ver}-${_ubuntu_rel}.tar.gz"
        'LICENSE')
sha512sums=(#'9475361e3e3ce0e8ad632003b6e41944d3750de3d5a1a774a2e9cb1f04fd0df8f27e6f47e31f08808c3f4506f683cbda53570617f1db1dd0657b07e4bac36a19'
            'ca7f7fcc137e1c5b34147128b12a0f6511c3ee0221f7007bbbe875a5b97ffad62dc526c46de07bd9c277267b9fc40a125ce5968c448e9bd2a97b35a0d43c7a01'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')

build() {
  cd "${srcdir}/workspace"

  # Apply Ubuntu's patches
  #patch -p1 -i "${srcdir}/mesa_${_ubuntu_ver}-${_ubuntu_rel}.diff"
  #cat "${srcdir}/mesa_${_ubuntu_ver}-${_ubuntu_rel}.diff" | \
  #  filterdiff --include="*/debian/*" | \
  #  patch -p1
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    `# --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast` \
    --with-gallium-drivers=nouveau,svga,swrast \
    `# --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast` \
    --with-dri-drivers=i915,i965,nouveau,swrast \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm,wayland,mir \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau

  make

  install -dm755  "${srcdir}/fakeinstall/"
  make DESTDIR="${srcdir}/fakeinstall/" install
}

#package_ati-dri-mir() {
#  pkgdesc="Mesa drivers for AMD/ATI Radeon"
#  depends=("mesa-libgl-mir=${pkgver}")
#  provides=("ati-dri=${pkgver}")
#  conflicts=('xf86-video-ati<6.9.0-6' 'ati-dri')
#
#  install -m755 -d ${pkgdir}/usr/lib/vdpau/
#  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_{r300,r600,radeonsi}.* ${pkgdir}/usr/lib/vdpau/
#
#  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
#  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/
#
#  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
#  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_{r300,r600,radeonsi}* ${pkgdir}/usr/lib/gallium-pipe/
#
#  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
#  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
#}

package_intel-dri-mir() {
  pkgdesc="Mesa drivers for Intel"
  depends=("mesa-libgl-mir=${pkgver}")
  provides=("intel-dri=${pkgver}")
  conflicts=('intel-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_nouveau-dri-mir() {
  pkgdesc="Mesa drivers for Nouveau"
  depends=("mesa-libgl-mir=${pkgver}")
  provides=("nouveau-dri=${pkgver}")
  conflicts=('nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_nouveau.* ${pkgdir}/usr/lib/vdpau/

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/nouveau-dri/"
}

package_svga-dri-mir() {
  pkgdesc="Gallium3D VMware guest GL driver"
  depends=('gcc-libs' 'libdrm' 'expat' 'libffi')
  provides=("svga-dri=${pkgver}")
  conflicts=('svga-dri')

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/vmwgfx_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_vmwgfx* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/svga-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/svga-dri/"
}

package_mesa-mir() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'systemd')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  provides+=("mesa=${pkgver}")
  conflicts+=('mesa')

  depends+=('mir-bzr')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 	${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_mesa-libgl-mir() {
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa-mir=${pkgver}")
  provides=("libgl=${pkgver}" "mesa-libgl=${pkgver}")
  replaces=('libgl' 'mesa-libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
