# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-system-compositor
_actual_ver=0.0.2
_extra_ver=+14.04.20140411
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="System compositor for Ubuntu"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-system-compositor"
license=('GPL')
groups=('unitynext')
depends=('boost' 'lightdm' 'mir' 'protobuf' 'qt5-base' 'xorg-server-mir')
makedepends=('cmake')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('b502a617ac42c9e033653bc111a9b59a6609b7e3a7e6f0c7a0f4cf76cbb83643fa8d2ae5dd7ef9b8c1e8e88b8b7f1a8db47447a15db51d8eae736bd7842cd457'
            '07bbed2a50df7b3ca4d1dffa4b3a9b668ade4f130ab2411042268589d43e33135a9e5c4f863931f54ec788c530e87c2da6a4d44b29d35e198a65e219a34c6f07')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  mv "${pkgdir}"/usr/{sbin,bin}/

  install -dm755 "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"
  install -m644 debian/10-unity-system-compositor.conf \
                "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"

  install -m755 debian/unity-system-compositor.sleep "${pkgdir}/usr/bin/"
  sed -i 's/sbin/bin/g' "${pkgdir}/usr/bin/unity-system-compositor.sleep"
}
