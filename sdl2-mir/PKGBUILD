# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_dirname=libsdl2.0-2.0.0~20130103

pkgname=sdl2-mir
pkgver=2.0.0
_ubuntu_rel=1ubuntu1+mir1
pkgrel=1
pkgdesc="Simple DirectMedia Layer"
arch=('i686' 'x86_64')
# http://www.libsdl.org/
url="http://ppa.launchpad.net/mir-team/staging/ubuntu/pool/main/libs/libsdl2/"
license=('custom')
groups=('unitynext')
depends=('alsa-lib' 'audiofile' 'directfb' 'libpulse' 'libx11' 'libxcursor' 'libxext' 'libxi' 'libxinerama' 'libxrandr' 'libxss' 'libxt' 'libxv' 'libxxf86vm' 'mesa' 'mesa-libgl' 'mir-bzr' 'systemd' 'tslib')

if grep -q 'i?86' <<< ${CARCH}; then
  depends+=('nasm')
fi

provides=("sdl2=${pkgver}")
conflicts=('sdl2')
source=("http://ppa.launchpad.net/mir-team/staging/ubuntu/pool/main/libs/libsdl2/libsdl2_${pkgver}.orig.tar.xz"
        "http://ppa.launchpad.net/mir-team/staging/ubuntu/pool/main/libs/libsdl2/libsdl2_${pkgver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('3888a6d726da8376fb7b78941052bbfcb1d82827229c02b1db8817fe1e667f01652349ae71d6f507672298f119d23e9b95b8aee38a3116578c1fec6777f0509f'
            'b8b4fdd7fa702c79ed5924c6ee953a5916eab8c771058fe44f9864cf3e7e3059e2cdcced379c45b8015d424bf440195c82121f374cac665d71f8345149d44e8d')

prepare() {
  cd "${srcdir}/${_dirname}"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${_dirname}"

  NASM=""
  if grep -q 'i?86' <<< ${CARCH}; then
    NASM="--enable-nasm"
  fi

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --enable-sdl-dlopen \
    --disable-loadso \
    --disable-nas \
    --disable-esd \
    --disable-arts \
    --disable-alsa-shared \
    --disable-pulseaudio-shared \
    --disable-x11-shared \
    --enable-video-directfb \
    --disable-video-mir \
    ${NASM}

  make
}

package() {
  cd "${srcdir}/${_dirname}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 "${srcdir}/debian/sdl2-config.1" \
                "${pkgdir}/usr/share/man/man1/"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 "${srcdir}/debian/copyright" \
                "${pkgdir}/usr/share/licenses/${pkgname}/"
}
