# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=sdl2-mir
pkgver=2.0.3
_ubuntu_ver=2.0.2+dfsg1
_ubuntu_rel=3ubuntu1
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
groups=('unitynext')
depends=('glibc' 'libxext' 'libxrender' 'libx11' 'libgl' 'libxcursor')
makedepends=('alsa-lib' 'mesa' 'libpulse' 'libxrandr' 'libxinerama' 'wayland' 'libxkbcommon')
optdepends=('alsa-lib: ALSA audio driver'
            'libpulse: PulseAudio audio driver')
provides=("sdl2=${pkgver}")
conflicts=('sdl2')
source=("http://www.libsdl.org/release/SDL2-${pkgver}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libsdl2_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('b6d2485154fbc07ab86a4305485e116d35fac25a97033b9e1c65ee0eb8251a18209cb1de3a914d9c0ddc53aa1ffac66c865b111d218e79eb5a10ed00dfec7540'
            'e3127142fc207f051df4f19b618eaff8b764264588add62edb4637230324cd790622c82bdb8e2d7d369e653f78b5e69bebb860ed645e55c531ae6b8eca9883b6')

prepare() {
  pushd SDL2-${pkgver}
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
  popd

  mkdir build
}

build() {
  pushd SDL2-${pkgver}
  ./autogen.sh
  popd

  cd build
  ../SDL2-${pkgver}/configure \
    --prefix=/usr \
    --enable-sdl-dlopen \
    --disable-arts \
    --disable-esd \
    --disable-nas \
    --enable-alsa \
    --enable-pulseaudio-shared \
    --enable-video-wayland \
    --enable-video-mir \
    --enable-mir-shared \
    --disable-rpath
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
  install -Dm644 ../SDL2-${pkgver}/COPYING.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
