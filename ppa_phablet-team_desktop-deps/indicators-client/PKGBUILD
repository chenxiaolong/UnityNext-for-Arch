# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_ubuntu_ver=0.27~raring6

pkgname=indicators-client
pkgver=${_ubuntu_ver/\~/.}
pkgrel=1
pkgdesc="Indicators and system settings client application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicators-client"
license=('LGPL')
groups=('unitynext')
depends=('glib2' 'indicator-battery' 'indicator-messages' 'indicator-time' 'libgl' 'mesa' 'networkmanager' 'python2' 'qmenumodel' 'qt5-base' 'qt5-declarative' 'ubuntu-ui-toolkit')
# For examples
depends+=('python2-dbus' 'python2-gobject' `# 'telephony-app'`)
# Autopilot
#depends+=('autopilot' 'autopilot-qt')
makedepends=('cmake')
source=("https://launchpad.net/~phablet-team/+archive/desktop-deps/+files/indicators-client_${_ubuntu_ver}.tar.gz"
        '0001_Python_2.patch'
        '0002_No_Autopilot.patch'
        '0003_qmldir.patch')
sha512sums=('87b23eea77a5805f39ba7d87727861a10465df817131e7663b452d23d66646e896730171e4282956ea9443a784d86c2fb794091b33f206f18ac06315da0729a5'
            '7a63f72796943fb6a32cb60b1a44787cde65562c15c1562969307303c543284fde81dda62fd7bd3f0d43f7385a05efa1403b0f4cbbd5af36a2de7eca68f9b040'
            '5d85ff99f5f44050982bd8373e75c738b36417882ca1da1402ef75d943f502ea7ecdfc1862639d8e11a389ba5a01306b63ed999f8a6d3416edd5a831cd462017'
            'f0c4d5481c53e55d40b229ae853f21b94377cb74337f170f32233d0cb4aaa8e5001045f3fc591ca8a4b08dbd099f04607b3d4c11982751ae7cc48681f887b921')

prepare() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  patch -p1 -i "${srcdir}/0001_Python_2.patch"
  patch -p1 -i "${srcdir}/0002_No_Autopilot.patch"
  patch -p1 -i "${srcdir}/0003_qmldir.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}/build/"
  ctest
}

package() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}/build/"
  make DESTDIR="${pkgdir}/" install
}
