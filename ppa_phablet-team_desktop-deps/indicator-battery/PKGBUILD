# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_ubuntu_ver=12.10.0phablet5~raring1

pkgname=indicator-battery
pkgver=${_ubuntu_ver/\~/}
pkgrel=1
pkgdesc="Indicator that displays battery charge level"
arch=('i686' 'x86_64')
url="https://launchpad.net/~phablet-team/+archive/desktop-deps/+packages"
license=('GPL')
groups=('unitynext')
depends=('glib2' 'systemd')
source=("https://launchpad.net/~phablet-team/+archive/desktop-deps/+files/indicator-battery_${_ubuntu_ver}.tar.gz")
sha512sums=('be926f01aaa8fefe64457d7b7c89835105ac386a7c43a93361976fa0f870a09f712f7513981c1c0cdf1f1ff5955ed0e1a8f1ebde70f9a3061b4f77427e10bf0d')

prepare() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac
}

build() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  make DESTDIR="${pkgdir}/" install
}
