# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_ubuntu_ver=12.10.0phablet3~raring1

pkgname=indicator-time
pkgver=${_ubuntu_ver/\~/.}
pkgrel=1
pkgdesc="Indicator that displays the current time"
arch=('i686' 'x86_64')
url="https://launchpad.net/~phablet-team/+archive/desktop-deps/+packages"
license=('GPL')
groups=('unitynext')
depends=('glib2')
source=("https://launchpad.net/~phablet-team/+archive/desktop-deps/+files/indicator-time_${_ubuntu_ver}.tar.gz")
sha512sums=('4a2e6740ccc9047973a1106ba3905f4433045a0ad09321d88aa5a2bdb67b65de7d2fdd8272aa736d62958ba1d44a9b29b7189af686b241b5aff26c27e886c417')

prepare() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac
}

build() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_ubuntu_ver}"
  make DESTDIR="${pkgdir}/" install
}
