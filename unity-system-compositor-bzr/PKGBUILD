# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-system-compositor-bzr
pkgver=43
pkgrel=1
pkgdesc="System compositor for Ubuntu"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-system-compositor"
license=('GPL')
groups=('unitynext')
depends=('boost' 'mir-bzr' 'protobuf')
makedepends=('bzr' 'cmake')
provides=('unity-system-compositor')
conflicts=('unity-system-compositor')
source=("unity-system-compositor::bzr+http://bazaar.launchpad.net/~unity-system-compositor-team/unity-system-compositor/trunk/")
sha512sums=('SKIP')

pkgver() {
  cd unity-system-compositor
  bzr revno
}

build() {
  cd "${srcdir}/unity-system-compositor"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/unity-system-compositor"
  make DESTDIR="${pkgdir}/" install

  mv "${pkgdir}"/usr/{sbin,bin}/

  install -dm755 "${pkgdir}/etc/lightdm/lightdm.conf.d/"
  install -m644 debian/10-unity-system-compositor.conf \
                "${pkgdir}/etc/lightdm/lightdm.conf.d/"

  install -m755 debian/unity-system-compositor.sleep "${pkgdir}/usr/bin/"
  sed -i 's/sbin/bin/g' "${pkgdir}/usr/bin/unity-system-compositor.sleep"
}
