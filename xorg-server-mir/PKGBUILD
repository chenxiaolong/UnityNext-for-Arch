# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: AndyRTR <andyrtr@archlinux.org>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

# Based on Arch Linux commit 15716748cce7684cbe0dd6a910efe89d4636bb3d

# AUR's parser...
pkgname=xorg-server-mir
pkgdesc="Xorg X server with XMir"

pkgbase=xorg-server-mir
: && pkgname=('xorg-server-mir'
              'xorg-server-xephyr-mir'
              'xorg-server-xdmx-mir'
              'xorg-server-xvfb-mir'
              'xorg-server-xnest-mir'
              'xorg-server-common-mir'
              'xorg-server-devel-mir')
pkgver=1.15.1
_ubuntu_rel=0ubuntu1
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('bigreqsproto'
             'compositeproto'
             'dmxproto'
             'dri2proto'
             'dri3proto'
             'fontsproto'
             'glproto'
             'inputproto'
             'libdmx'
             'libgcrypt'
             'libpciaccess'
             'libx11'
             'libxaw'
             'libxfont'
             'libxi'
             'libxkbfile'
             'libxmu'
             'libxrender'
             'libxres'
             'libxshmfence'
             'libxtst'
             'libxv'
             'mesa'
             'mesa-libgl'
             'mir' # Actually depends on libmirclient
             'pixman'
             'presentproto'
             'randrproto'
             'recordproto'
             'renderproto'
             'resourceproto'
             'scrnsaverproto'
             'videoproto'
             'xcb-util'
             'xcb-util-image'
             'xcb-util-keysyms'
             'xcb-util-wm'
             'xcmiscproto'
             'xf86dgaproto'
             'xf86driproto'
             'xineramaproto'
             'xorg-font-util'
             'xorg-xkbcomp'
             'xorg-util-macros'
             'xtrans')
options=('!libtool')
source=("${url}/releases/individual/xserver/xorg-server-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xorg-server_${pkgver}-${_ubuntu_rel}.diff.gz"
        'autoconfig-nvidia.patch'
        'autoconfig-sis.patch'
        'xvfb-run'
        'xvfb-run.1'
        '10-quirks.conf'
        'CVE-2013-6424.diff')
sha512sums=('93c9831d354f926243d047bc05a057484e69914410914193d7e8e8b4724ee7b74f57476f1ba59cae5a845e69565ff5529ec25e45322200de9aa38cdf6394257f'
            'e42fcde1546642647b0fe788260038085984ebfab89b563b6abe018875a4e7e8d0df4fffc28ebf64b795003e04c11143ce234b3eaf69da051e7bbac5550032d7'
            '4dcaa60fbfc61636e7220a24a72bba19984a6dc752061cb40b1bd566c0e614d08927b6c223ffaaaa05636765fddacdc3113fde55d25fd09cd0c786ff44f51447'
            '3211dbc6d8a84f3f6a77230a8e5fe13355b441768d864c2b3037b893be39f7b8d581c2a5db1e04f399ce19c4ced973b64eaad563d970ee9c7b8a93cb23a0aa18'
            'ca1cda27016f7c269cbdecc45da36255afeef5c1973cc484544f9dfbf56ed1868365c93a4c7f93e3a23e5322f084ec0cdd137e15b43872aae7f0c03040028ce6'
            'de5e2cb3c6825e6cf1f07ca0d52423e17f34d70ec7935e9dd24be5fb9883bf1e03b50ff584931bd3b41095c510ab2aa44d2573fd5feaebdcb59363b65607ff22'
            '9a1a5568be751435daea720cfc4bad209d62545cc10ea2f49113c41669c8130809a680492256ef331757fe8539d2e0e5e9e67a36f7c48c8d92d9b3e957d28fa2'
            '1d2084d875ed5c619be724d5983b984c19bf64e9dcc5157f2adbbd6b42403fc034fb4df31b66a1283e80279a3c4c2a91fb45d81569c249c4bd9f9016eb70d35e')

prepare() {
  cd "${srcdir}/xorg-server-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/xorg-server_${pkgver}-${_ubuntu_rel}.diff"

  # Disable patches
    sed -i '/03_static-nettle.diff/d' debian/patches/series

  for i in $(grep -v '#' debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "debian/patches/${i}"
  done

  # Use nouveau/nv/nvidia drivers for nvidia devices
  #patch -p1 -i "${srcdir}/autoconfig-nvidia.patch"

  # Use unofficial imedia SiS driver for supported SiS devices
  #patch -p0 -i "${srcdir}/autoconfig-sis.patch"

  # Fix CVE-2013-6424 (FS#38401)
  #patch -p1 -i "${srcdir}/CVE-2013-6424.diff"
}

build() {
  cd "${srcdir}/xorg-server-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --enable-ipv6 \
    --enable-dri \
    --enable-dmx \
    --enable-xvfb \
    --enable-xmir \
    --enable-xnest \
    --enable-composite \
    --enable-xcsecurity \
    --enable-xorg \
    --enable-xephyr \
    --enable-glx-tls \
    --enable-kdrive \
    --enable-kdrive-evdev \
    --enable-kdrive-kbd \
    --enable-kdrive-mouse \
    --enable-install-setuid \
    --enable-config-udev \
    --disable-config-dbus \
    --enable-record \
    --disable-xfbdev \
    --disable-xfake \
    --disable-static \
    --sysconfdir=/etc/X11 \
    --localstatedir=/var \
    --with-xkb-path=/usr/share/X11/xkb \
    --with-xkb-output=/var/lib/xkb \
    --with-fontrootdir=/usr/share/fonts

  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-mir() {
  pkgdesc="Xorg server common files"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc')
  provides=("xorg-server-common=${pkgver}")
  conflicts=('xorg-server-common')

  cd "${srcdir}/xorg-server-${pkgver}"
  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-common"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xorg-server-common"
  
  make -C xkb DESTDIR="${pkgdir}" install-data

  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/Xserver.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"
}

package_xorg-server-mir() {
  pkgdesc="Xorg X server with XMir"
  depends=(libxdmcp libxfont libpciaccess libdrm pixman libgcrypt libxau xorg-server-common xf86-input-evdev libxshmfence)
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  # see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  # and /usr/lib/pkgconfig/xorg-server.pc in xorg-server-devel pkg
  provides=('X-ABI-VIDEODRV_VERSION=15' 'X-ABI-XINPUT_VERSION=20' 'X-ABI-EXTENSION_VERSION=8.0' 'x-server')
  groups=('xorg')
  conflicts=('nvidia-utils<=331.20')

  provides+=("xorg-server=${pkgver}")
  conflicts+=('xorg-server')

  cd "${srcdir}/xorg-server-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "${pkgdir}/var"

  rm -f "${pkgdir}/usr/share/man/man1/Xserver.1"
  rm -f "${pkgdir}/usr/lib/xorg/protocol.txt"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server/COPYING"

  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share/aclocal"
}

package_xorg-server-xephyr-mir() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont libgl libgcrypt libxv pixman xorg-server-common-mir 'xcb-util-image' 'xcb-util-wm' 'xcb-util-keysyms')
  provides=("xorg-server-xephyr=${pkgver}")
  conflicts=('xorg-server-xephyr')

  cd "${srcdir}/xorg-server-${pkgver}/hw/kdrive"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xephyr/COPYING"
}

package_xorg-server-xvfb-mir() {
  pkgdesc="Virtual framebuffer X server"
  depends=(libxfont libxdmcp libxau libgcrypt pixman xorg-server-common-mir xorg-xauth)
  provides=("xorg-server-xvfb=${pkgver}")
  conflicts=('xorg-server-xvfb')

  cd "${srcdir}/xorg-server-${pkgver}/hw/vfb"
  make DESTDIR="${pkgdir}" install

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xvfb/COPYING"
}

package_xorg-server-xnest-mir() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont libxext libgcrypt pixman xorg-server-common-mir)
  provides=("xorg-server-xnest=${pkgver}")
  conflicts=('xorg-server-xnest')

  cd "${srcdir}/xorg-server-${pkgver}/hw/xnest"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xnest/COPYING"
}

package_xorg-server-xdmx-mir() {
  pkgdesc="Distributed Multihead X Server and utilities"
  depends=(libxfont libxi libgcrypt libxaw libxrender libdmx libxfixes pixman xorg-server-common-mir)
  provides=("xorg-server-xdmx=${pkgver}")
  conflicts=('xorg-server-xdmx')

  cd "${srcdir}/xorg-server-${pkgver}/hw/dmx"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xdmx/COPYING"
}

package_xorg-server-devel-mir() {
  pkgdesc="Development files for the X.Org X server"
  depends=(# see pkgdir/usr/lib/pkgconfig/xorg-server.pc
           xproto randrproto renderproto xextproto inputproto kbproto 
           fontsproto pixman videoproto xf86driproto glproto 
           mesa dri2proto dri3proto xineramaproto libpciaccess
           resourceproto scrnsaverproto presentproto
           # not technically required but almost every Xorg pkg needs it to build
           xorg-util-macros)
  provides=("xorg-server-devel=${pkgver}")
  conflicts=('xorg-server-devel')

  cd "${srcdir}/xorg-server-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
  rm -rf "${pkgdir}/usr/share/doc"
  rm -rf "${pkgdir}/usr/share/X11"
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-devel/COPYING"
}
