# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on the official Arch Linux xorg-server package

pkgname=xorg-server-mir
pkgver=1.13.2
_ubuntu_rel=0ubuntu2+xmir1

# AUR fix
pkgrel=1.0ubuntu2.xmir1
true && pkgrel=1.${_ubuntu_rel/+/.}

pkgdesc="Xorg server with XMir"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('dri2proto'
         'fontsproto'
         'glproto'
         'inputproto'
         'kbproto'
         'libdmx'
         'libdrm'
         'libgcrypt'
         'libgl'
         'libpciaccess'
         'libxau'
         'libxaw'
         'libxdmcp'
         'libxext'
         'libxfixes'
         'libxfont'
         'libxi'
         'libxrender'
         'libxv'
         'mesa'
         'mir-bzr' # Actually depends on libmirclient
         'pixman'
         'randrproto'
         'renderproto'
         'resourceproto'
         'scrnsaverproto'
         'videoproto'
         'xextproto'
         'xf86-input-evdev'
         'xf86driproto'
         'xineramaproto'
         'xkeyboard-config'
         'xorg-fonts-misc'
         'xorg-setxkbmap'
         'xorg-util-macros'
         'xorg-xkbcomp'
         'xproto')
makedepends=('bigreqsproto'
             'compositeproto'
             'dmxproto'
             'libx11'
             'libxkbfile'
             'libxmu'
             'libxres'
             'libxtst'
             'mesa-libgl'
             'recordproto'
             'xcmiscproto'
             'xf86dgaproto'
             'xtrans'
             'xorg-font-util'
             'xorg-util-macros')
# see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
provides=('X-ABI-VIDEODRV_VERSION=13'
          'X-ABI-XINPUT_VERSION=18'
          'X-ABI-EXTENSION_VERSION=7.0'
          'x-server')

addprovides() {
  for i in ${@}; do
    provides+=("${i}=${pkgver}")
    conflicts+=("${i}")
  done
}

addprovides xorg-server xorg-server-xephyr xorg-server-xdmx xorg-server-xvfb xorg-server-xnest xorg-server-common xorg-server-devel

backup=('etc/X11/xorg.conf.d/10-evdev.conf'
        'etc/X11/xorg.conf.d/10-quirks.conf')
options=('!libtool')
source=(${url}/releases/individual/xserver/xorg-server-${pkgver}.tar.bz2
        https://launchpad.net/~mir-team/+archive/staging/+files/xorg-server_${pkgver}-${_ubuntu_rel}.diff.gz
        autoconfig-nvidia.patch
        autoconfig-sis.patch
        xserver-1.6.0-less-acpi-brokenness.patch
        bug59825.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf
        git-fixes.patch)
sha512sums=('bbd76f800eaf0247486c19992716d231e69d57c3a36235d684e6503ff5316bbbd2dda6b44f7697920145566f0e1a3636725170e8ded853061cc89409f3b1011a'
            'a6f059fa20fc5213cfed942681398016ec9f47fd8b0d0d7a925ae3caf784eba8f5c9e13c13eb15fcbcddd3bb9da53b6bc79b041a705fb69736629d20c6f84e2a'
            '4dcaa60fbfc61636e7220a24a72bba19984a6dc752061cb40b1bd566c0e614d08927b6c223ffaaaa05636765fddacdc3113fde55d25fd09cd0c786ff44f51447'
            '3211dbc6d8a84f3f6a77230a8e5fe13355b441768d864c2b3037b893be39f7b8d581c2a5db1e04f399ce19c4ced973b64eaad563d970ee9c7b8a93cb23a0aa18'
            '4ff7c764b9c5121de6e669928e8f8fab64098271b288c4c7c7a073d606000a724b46013a3224b961785a51a1e54903bcead42359d04c7dee7248a98f93f38952'
            '53a49cd933685db255b30eb10bd558e1ff39452bad84734c99df56725a8f79f228634a3771c495780bb09267da56fe6ed214fa1b75f60f44c9ef7c05d6882039'
            'ca1cda27016f7c269cbdecc45da36255afeef5c1973cc484544f9dfbf56ed1868365c93a4c7f93e3a23e5322f084ec0cdd137e15b43872aae7f0c03040028ce6'
            'de5e2cb3c6825e6cf1f07ca0d52423e17f34d70ec7935e9dd24be5fb9883bf1e03b50ff584931bd3b41095c510ab2aa44d2573fd5feaebdcb59363b65607ff22'
            '9a1a5568be751435daea720cfc4bad209d62545cc10ea2f49113c41669c8130809a680492256ef331757fe8539d2e0e5e9e67a36f7c48c8d92d9b3e957d28fa2'
            'c1038ec895c781ab16d763d0221ce7bb61a9177e0ef64b8e1b8218ac4f563903e92f1640cfb9b00e57b7c1b2296d9a4e3b356b2eb0481af8116513abff9f942c')

build() {
  cd "${srcdir}/xorg-server-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/xorg-server_${pkgver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

  # Use nouveau/nv/nvidia drivers for nvidia devices
  patch -p1 -i "${srcdir}/autoconfig-nvidia.patch"

  # Use unofficial imedia SiS driver for supported SiS devices
  patch -p0 -i "${srcdir}/autoconfig-sis.patch"

  # https://bugs.freedesktop.org/show_bug.cgi?id=59825 - fixes crash with vboxvideo
  patch -p1 -i "${srcdir}/bug59825.patch"

  # From Fedora. Do not build acpid code, it is buggy and we do not need it
  patch -p1 -i "${srcdir}/xserver-1.6.0-less-acpi-brokenness.patch"

  # post release git fixes
  patch -p1 -i "${srcdir}/git-fixes.patch"

  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --enable-ipv6 \
    --enable-dri \
    --enable-dmx \
    --enable-xvfb \
    --enable-xmir \
    --enable-xnest \
    --enable-composite \
    --enable-xcsecurity \
    --enable-xorg \
    --enable-xephyr \
    --enable-glx-tls \
    --enable-kdrive \
    --enable-kdrive-evdev \
    --enable-kdrive-kbd \
    --enable-kdrive-mouse \
    --enable-install-setuid \
    --enable-config-udev \
    --disable-config-dbus \
    --enable-record \
    --disable-xfbdev \
    --disable-xfake \
    --disable-static \
    --sysconfdir=/etc/X11 \
    --localstatedir=/var \
    --with-xkb-path=/usr/share/X11/xkb \
    --with-xkb-output=/var/lib/xkb \
    --with-fontrootdir=/usr/share/fonts

  make
}

package() {
  cd "${srcdir}/xorg-server-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/Xserver.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"
}
