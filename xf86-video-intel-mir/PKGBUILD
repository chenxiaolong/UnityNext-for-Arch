# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: AndyRTR <andyrtr@archlinux.org>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

# Based on Arch Linux commit cb8570727570ba5cfcdceebd56b4a1524dff64d9

pkgname=xf86-video-intel-mir
_ubuntu_ver=2.99.910
_ubuntu_rel=0ubuntu1
pkgver=2.99.911
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
groups=('xorg-drivers' 'xorg')
depends=('intel-dri-mir' 'libxvmc' 'pixman' 'xcb-util>=0.3.9' 'glamor-egl')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=15' 'libx11' 'libxrender'
             # additional deps for intel-virtual-output
            'libxrandr' 'libxinerama' 'libxcursor' 'libxtst')
makedepends+=('xorg-server-mir')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16'
           'xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xf86-video-i810' 'xf86-video-intel-legacy')
provides+=("xf86-video-intel=${pkgver}")
conflicts+=('xf86-video-intel')
options=('!libtool')
install=xf86-video-intel.install
source=("${url}/archive/individual/driver/xf86-video-intel-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-video-intel_${_ubuntu_ver}-${_ubuntu_rel}.diff.gz"
        'sna-avoid-discarding-damage.patch')
sha512sums=('73a6f44f80322c3812e8d36503e42cf00e19028d64244447defecd514c17fa5ed1cacaee0c82b96d4ae8d9108e68e446ca2bbd628406a79528ea42194845b20b'
            'ca97df40d39dd60e85a3305d896fe4c8857907b9537b882fdd98ff0ffd76d9e878af893dd2c465f94239e58717b7d7289370a9a847acf6d821a11ce4f81445b7'
            'b1e72895493715da4259517242055c755e87e22ac660612eabb26c6da308d5b512406b19ab077c6fcffe031db104853d6349db80deef1f48dd5eb8f4c707c147')

prepare() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/xserver-xorg-video-intel_${_ubuntu_ver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "debian/patches/${i}"
  done

  # Fix FS#39747 (merged upstream)
  patch -Np1 -i ../sna-avoid-discarding-damage.patch
}

build() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"
  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-glamor
  make
}

check() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
