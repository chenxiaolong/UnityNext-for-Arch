# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: AndyRTR <andyrtr@archlinux.org>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

# Based on Arch Linux commit d03f5fb77df413017821f492aa81e5d68def7e48

pkgname=xf86-video-intel-mir
_ubuntu_rel=4ubuntu2
pkgver=2.21.14
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
groups=('xorg-drivers' 'xorg')
depends=('intel-dri-mir' 'libxvmc' 'xcb-util>=0.3.9' 'systemd')
# pacman... X-ABI-VIDEODRV_VERSION=14
makedepends=('xorg-server-mir' 'xorg-server-devel-mir' 'resourceproto' 'scrnsaverproto' 'libx11' 'libxrender')
makedepends+=('xorg-server-mir')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xorg-server<1.14.0' 'X-ABI-VIDEODRV_VERSION<14' 'X-ABI-VIDEODRV_VERSION>=15'
           'xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xf86-video-i810' 'xf86-video-intel-legacy')
provides+=("xf86-video-intel=${pkgver}")
conflicts+=('xf86-video-intel')
options=('!libtool')
install=xf86-video-intel.install
source=("${url}/archive/individual/driver/xf86-video-intel-${pkgver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-video-intel_${pkgver}-${_ubuntu_rel}.diff.gz")
sha512sums=('34e7ecc5a99b6a0f711b154755c8dc5dabb56f04150ca7f02fd1a715347dd9cfc5851b2e3763c4e89b57f3e347975ed17630630e4e07efc690b2f314e6ab0f3a'
            '0ee24484b03f8d4461fa7f94fe383271e73d46ace40f362ad31c8372af6ce648b525c328ab9dcde38529c11c3c417ff45ba877b8d73931551b229bc808b0b006')

prepare() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/xserver-xorg-video-intel_${pkgver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"
  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --enable-dri \
    --with-default-accel=sna
  make
}

package() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
